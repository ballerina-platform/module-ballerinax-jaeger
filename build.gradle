/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'checkstyle'
    id "com.github.spotbugs" version "${githubSpotbugsVersion}"
    id "com.github.johnrengelman.shadow" version "${githubJohnrengelmanShadowVersion}"
    id "de.undercouch.download" version "${underCouchDownloadVersion}"
    id "net.researchgate.release" version "${researchgateReleaseVersion}"
    id "jacoco"
}

allprojects {
    group = project.group
    version = project.version

    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
        }
        maven {
            url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
        }
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
        maven {
            url 'https://maven.pkg.github.com/ballerina-platform/*'
            credentials {
                username System.getenv('packageUser')
                password System.getenv('packagePAT')
            }
        }
    }

    ext {
        snapshotVersion= '-SNAPSHOT'
        timestampedVersionRegex = '.*-\\d{8}-\\d{6}-\\w.*\$'
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    configurations {
        jbalTools {
            transitive = false
        }
        ballerinaStdLibs
    }

    checkstyle {
        toolVersion '10.12.1'
    }

    spotbugsMain {
        def classLoader = plugins["com.github.spotbugs"].class.classLoader
        def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
        def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
        it.effort = SpotBugsEffort.MAX
        it.reportLevel = SpotBugsConfidence.LOW
        it.reports {
            xml.enabled false
            html.enabled true
        }
        def excludeFile = file('spotbugs-exclude.xml')
        if (excludeFile.exists()) {
            it.excludeFilter = excludeFile
        }
    }

    spotbugsTest {
        def classLoader = plugins["com.github.spotbugs"].class.classLoader
        def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
        def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
        it.effort = SpotBugsEffort.MAX
        it.reportLevel = SpotBugsConfidence.LOW
        it.reports {
            xml.enabled false
            html.enabled true
        }
        def excludeFile = file('spotbugs-test-exclude.xml')
        if (excludeFile.exists()) {
            it.excludeFilter = excludeFile
        }
    }

    tasks.withType(Checkstyle) {
        dependsOn ":build-config:checkstyle:downloadFiles"
        configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
        exclude '**/module-info.java'
    }
}

def moduleVersion = project.version.replace("-SNAPSHOT", "")

def executeBalCommand(String command, String dir, env = "") {
    try {
        exec {
            workingDir dir
            environment environment: env
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "bal.bat ${command} && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "bal ${command}"
            }
        }
    } catch (Exception e) {
        println("bal command failed. " + e.message)
        throw e
    }
}

release {
    failOnPublishNeeded = false
    failOnSnapshotDependencies = true

    buildTasks = ['build']
    versionPropertyFile = 'gradle.properties'
    tagTemplate = 'v$version'

    git {
        requireBranch = "release-${moduleVersion}"
        pushToRemote = 'origin'
    }
}

task build {
    dependsOn('jaeger-extension-ballerina:build')
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn('jaeger-extension-ballerina:extractBallerinaClassFiles')
    dependsOn('jaeger-extension-native:copyJavaClassFiles')

    executionData fileTree(project.rootDir.absolutePath).include("**/*.exec")
    additionalClassDirs files("${buildDir}/classes")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.required = true
        html.required = true
        xml.destination = new File("${buildDir}/reports/jacoco/report.xml")
        html.destination = new File("${buildDir}/reports/jacoco/report.html")
    }

    onlyIf = {
        true
    }
}
